import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    
    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "org.zephyrsoft.trackworktime"
        minSdkVersion 14
        targetSdkVersion 27
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "TrackWorkTime_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    implementation 'ch.acra:acra:4.9.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.darwinsys:hirondelle-date4j:1.5.1'
    implementation 'com.getpebble:pebblekit:3.1.0@aar'
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
}
