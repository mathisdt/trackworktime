import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    lint {
        abortOnError false
    }

    namespace 'org.zephyrsoft.trackworktime'

    defaultConfig {
        if (app_suffix == null || app_suffix.trim().isEmpty()) {
            applicationId "org.zephyrsoft.trackworktime"
            resValue "string", "app_name", "${app_title}"
            buildConfigField "String", "APP_NAME", "\"${app_title}\""
        } else {
            applicationId "org.zephyrsoft.trackworktime_" + app_suffix.replaceAll("[^a-zA-Z0-9]", "")
            resValue "string", "app_name", "${app_title} ${app_suffix}"
            buildConfigField "String", "APP_NAME", "\"${app_title} ${app_suffix}\""
        }

        versionCode 66                      // increment after every release (for the next development cycle)
        versionName '1.1.22-SNAPSHOT'       // change with every release

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        minSdkVersion 21
        targetSdkVersion 33
    }

    compileSdkVersion 33

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                obfuscate false
                optimizeCode false
            }
        }
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "TrackWorkTime_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    def acraVersion = "5.11.1"

    implementation 'com.github.mathisdt:timezonepicker:cb06a1b0f9'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'com.google.guava:guava:32.1.2-android'
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    implementation 'org.tinylog:tinylog:1.3.6'

    runtimeOnly 'androidx.lifecycle:lifecycle-viewmodel:2.6.1'
    runtimeOnly 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.5'
    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'com.tngtech.archunit:archunit:1.0.1'

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    modules {
        module("com.google.guava:listenablefuture") {
            replacedBy("com.google.guava:guava", "listenablefuture is part of guava")
        }
    }
}
