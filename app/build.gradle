import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "org.zephyrsoft.trackworktime"
        minSdkVersion 19
        targetSdkVersion 29

        versionCode 54                      // increment with every release
        versionName '1.1.10-SNAPSHOT'        // change with every release
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                obfuscate false
                optimizeCode false
            }
        }
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "TrackWorkTime_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    def acraVersion = "5.8.4"

    implementation project(':timezonepicker')

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.asynclayoutinflater:asynclayoutinflater:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.work:work-runtime:2.6.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.getpebble:pebblekit:4.0.1@aar'
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    implementation 'org.tinylog:tinylog:1.3.6'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.threeten:threetenbp:1.5.1'
}
